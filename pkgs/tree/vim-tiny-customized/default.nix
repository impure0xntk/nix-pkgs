# https://github.com/dpausp/dotfiles/blob/588c6f8b74efbf8a3c57efccf4e1885eb6f032d1/.nixpkgs/config.nix#L65
# https://hardselius.github.io/
{ pkgs, ... }:
((pkgs.vim-full.overrideAttrs(prev: {
  # Replace vimrc fo avoiding conflict with vim-tiny("syntax on" is unavailable)
  # And also disable plugin-loader.
  # https://github.com/NixOS/nixpkgs/blob/nixos-24.05/pkgs/applications/editors/vim/full.nix
  # https://github.com/NixOS/nixpkgs/blob/nixos-24.05/pkgs/applications/editors/vim/default.nix
  postInstall =
  let
    archLinuxVimrc = pkgs.fetchurl {
      url = "https://raw.githubusercontent.com/archlinux/svntogit-packages/68f6d131750aa778807119e03eed70286a17b1cb/trunk/archlinux.vim";
      hash = "sha256-DPi0JzIRHQxmw5CKdtgyc26PjcOr74HLCS3fhMuGLqI=";
    };
  in prev.postInstall + ''
    unlink "$out"/share/vim/vimrc
    cat <<EOF > "$out"/share/vim/vimrc
${builtins.readFile archLinuxVimrc}

if filereadable("/etc/vimrc")
  source /etc/vimrc
elseif filereadable("/etc/vim/vimrc")
  source /etc/vim/vimrc
endif

EOF
    '';
})).override {
  features = "tiny"; # small or higher is required for vimrc that is generated by NixOS
  config = {
    netbeans = false;
    vim = {
      lua = false;
      perl = false;
      python = false;
      ruby = false;
      tcl = false;
      multibyte = true;
      cscope = false;
      ftNix = false;
      gui = "no";
    };
  };
  libX11 = null;
  libXext = null;
  libSM = null;
  libXpm = null;
  libXt = null;
  libXaw = null;
  libXau = null;
  libXmu = null;
  libICE = null;
  gtk2-x11 = null;
  gtk3-x11 = null;
  lua.pkgs.isLuaJIT = false;
}).customize {
  name = "vim-tiny-customized";
  vimrcConfig.customRC = "";
}
